/* build a distribution with:
* ./gradlew distZip
* run tests and report with:
* ./gradlew check
*/
plugins {
    id 'groovy'
    id 'application'
    id "net.codebuilders.lazybones-templates" version "${lazybonesGradlePluginVersion}"
}

archivesBaseName = 'skeletal-app'

applicationName = "skeletal"
mainClassName = "uk.co.cacoethes.lazybones.LazybonesMain"
// make sure to update the asciidoc guide headers also
version = "0.19.0"

// These settings mimic the old client VM behavior. Should result in faster startup.
applicationDefaultJvmArgs = ["-XX:+TieredCompilation", "-XX:TieredStopAtLevel=1", "-XX:CICompilerCount=3"]


ext {
    groovyVersion = "4.0.29"
    cachePath = new File(System.getProperty("user.home"), ".skeletal/templates").absolutePath
    testWorkDir = file("$buildDir/testWork").path
}

sourceSets {
    integTest {
        groovy {
            srcDir "src/integ/groovy"
            compileClasspath += sourceSets.main.output
        }
        resources {
            srcDir "src/integ/resources"
        }
    }
}

configurations {
    integTestImplementation.resolutionStrategy {
        force "org.apache.groovy:groovy:${groovyVersion}"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.apache.groovy:groovy:${groovyVersion}"

    implementation "commons-io:commons-io:2.7"
    implementation "org.apache.groovy:groovy-xml:${groovyVersion}"
    implementation "org.apache.groovy:groovy-json:${groovyVersion}"
    implementation "org.apache.groovy:groovy-templates:${groovyVersion}"
    implementation "org.apache.commons:commons-compress:1.5"
    implementation "net.sf.jopt-simple:jopt-simple:4.4"
    implementation "org.ini4j:ini4j:0.5.2"
    implementation "net.codebuilders:groovy-handlebars-engine:0.3.0-M1"
    // artifactory integration
    implementation("org.apache.httpcomponents:httpcore:4.4.16")
    implementation("org.apache.httpcomponents:httpclient:4.5.14")

    implementation("org.apache.maven:maven-artifact:3.9.6")
    implementation "org.jfrog.artifactory.client:artifactory-java-client-services:2.17.0"
    implementation "org.apache.maven.indexer:indexer-core:7.1.2"
    implementation "com.opencsv:opencsv:5.5.1"

    implementation 'org.slf4j:slf4j-api:2.0.17'
    runtimeOnly 'org.slf4j:slf4j-simple:2.0.17'

    runtimeOnly "org.apache.ivy:ivy:2.3.0"

    testImplementation 'org.spockframework:spock-core:2.4-M6-groovy-4.0'
    testImplementation 'junit:junit:4.13.2'

    // you can use testRuntimeClasspath if you don't want to use spock-report-specific features in your Specs
    testImplementation("com.athaydes:spock-reports:2.5.1-groovy-4.0") {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }

    integTestImplementation "org.apache.groovy:groovy:${groovyVersion}",
            "org.apache.groovy:groovy-json:${groovyVersion}",
            "commons-io:commons-io:2.7",
            "co.freeside:betamax:1.1.2",
            "org.littleshoot:littleproxy:1.1.0-beta1"

    integTestImplementation 'org.spockframework:spock-core:2.4-M7-groovy-4.0'

    // you can use testRuntimeClasspath if you don't want to use spock-report-specific features in your Specs
    integTestImplementation("com.athaydes:spock-reports:2.5.1-groovy-4.0") {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }
    // if you don't already have slf4j-api and an implementation of it in the classpath, add this!
    integTestImplementation 'org.slf4j:slf4j-api:2.0.17'
    integTestImplementation 'org.slf4j:slf4j-simple:2.0.17'

    // spock-reports in groovy 4
    integTestImplementation "org.apache.groovy:groovy-xml:${groovyVersion}"
    integTestImplementation "org.apache.groovy:groovy-json:${groovyVersion}"
    integTestImplementation "org.apache.groovy:groovy-templates:${groovyVersion}"

}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

tasks.withType(GroovyCompile).configureEach {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}


jar {
    manifest {
        attributes("Implementation-Title": "Skeletal", "Implementation-Version": project.version)
    }
}

lazybones {
    templateDirs = files(file("${projectDir}/src/integTest/templates").listFiles())

    template "subtemplates-tmpl" includes "controller", "entity", "bad"
    template "test-handlebars", {
        version = "0.1.1"
    }
}

packageAllTemplates.dependsOn("packageTemplate-Oops-stuff")
installAllTemplates.dependsOn("installTemplate-Oops-stuff")

task configureCachePath {
    project.cachePath = testWorkDir + "/template-cache"
    project.extensions.lazybones.installDir = new File(project.cachePath)
}

tasks.withType(Test).configureEach {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()

    // spock reports
    systemProperty 'com.athaydes.spockframework.report.showCodeBlocks', true
}

// for a single test, you can run:
// ./gradlew integTest --tests=uk.co.cacoethes.lazybones.ConfigFunctionalSpec
task integTest(type: Test) {
    dependsOn "installDist", "configureCachePath", "installAllTemplates"

    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    shouldRunAfter test

    ignoreFailures = true

    systemProperty "integration.test", "true"
    systemProperty "lazybones.testWorkDir", testWorkDir
    systemProperty "lazybones.installDir", installDist.destinationDir.path
    systemProperty "lzbtest.expected.version", version

    // Allows us to disable tests that don't work on Drone.io, such as the
    // CreateFunctionalSpec feature test for --with-git.
    systemProperty "lazybones.config.file",
            new File(sourceSets.integTest.output.resourcesDir, "test-config.groovy").absolutePath

    // def cachePath

    // Use a custom one on local machines to avoid polluting the developer's own cache.
    systemProperty "lazybones.cache.dir", project.cachePath

    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath

    include "**/*Spec*"
    exclude "**/Abstract*Spec*"

    // useJUnitPlatform()
}

task packageReports(type: Zip) {
    dependsOn "check"
    from "build/reports"
    archiveFileName = "reports.zip"
    destinationDirectory = buildDir
}

integTest.finalizedBy packageReports

check.dependsOn("test", "integTest")
distZip.dependsOn("check")
