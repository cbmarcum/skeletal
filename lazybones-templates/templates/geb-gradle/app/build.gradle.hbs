/*
 * This source file was generated by the Skeletal project creation tool.
 * https://github.com/cbmarcum/skeletal
 */

plugins {
    // Apply the groovy Plugin to add support for Groovy.
    id 'groovy'

    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
}

// group and version used for application
group = "{{project_group}}"
version = "{{project_version}}"

ext {
    // The drivers we want to use
    drivers = ["firefox", "chrome", "chromeHeadless"]
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform(libs.junit.bom)

    implementation libs.groovy
    implementation libs.geb.core
    // Selenium Drivers for main app
    implementation libs.selenium.chrome.driver
    implementation libs.selenium.firefox.driver

    // Geb
    testImplementation libs.geb.spock
    testImplementation libs.geb.junit5

    // JUnit 5
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.platform.launcher

    // Selenium Drivers
    testImplementation libs.selenium.chrome.driver
    testImplementation libs.selenium.firefox.driver
    testImplementation libs.selenium.devtools.v143

    // Spock Reports
    // you can use testRuntimeClasspath if you don't want to use spock-report-specific features in your Specs
    testImplementation( libs.spock.reports ) {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }
    testImplementation libs.groovy.xml
    testImplementation libs.groovy.json
    testImplementation libs.groovy.templates
    // for No SLF4J providers were found (if not otherwise provided)
    testImplementation libs.sfl4j.api
    testRuntimeOnly libs.sfl4j.simple
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
// archive name before version
archiveBaseName = '{{project_name}}'
}

application {
// Define the main class for the application.
mainClass = '{{project_package}}.{{project_class_name}}'
// Define the application command name
applicationName = '{{project_name}}'
}

// Create one test task per driver (firefoxTest, chromeTest, chromeHeadlessTest)
drivers.eachWithIndex { driver, index ->
    tasks.register("${driver}Test", Test) {
        group = JavaBasePlugin.VERIFICATION_GROUP
        outputs.upToDateWhen { false }  // Always run
        useJUnitPlatform()

        systemProperty "geb.build.reportsDir", reporting.file("geb/$name")
        systemProperty "geb.env", driver
        // spock reports
        systemProperty 'com.athaydes.spockframework.report.outputDir', "build/spock-reports/${driver}Test"

        if (index > 0) {
            mustRunAfter "${drivers[index-1]}Test"
        }
        // no parallel execution
        maxParallelForks = 1
    }
}

// Disable the default 'test' task â€” we run the driver-specific ones instead
test {
    enabled = false
}

// Make the default 'check' task depend on all driver-specific test tasks
check {
    dependsOn drivers.collect { tasks["${it}Test"] }
}

tasks.withType(Test) {
    maxHeapSize = "1g"
    jvmArgs '-XX:MaxMetaspaceSize=128m'

    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
    // spock reports
    systemProperty 'com.athaydes.spockframework.report.showCodeBlocks', true
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.memoryMaximumSize = '256m'
}
