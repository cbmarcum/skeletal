/*
 * This source file was generated by the Skeletal project creation tool.
 * https://github.com/cbmarcum/skeletal
 */
package {{project_package}}

import geb.Browser
import geb.driver.CachingDriverFactory
import geb.navigator.Navigator
import org.example.pages.TheBookOfGebPage
import org.openqa.selenium.Dimension

import org.example.pages.GebHomePage
import org.openqa.selenium.WebDriver
import org.openqa.selenium.WebElement

class {{project_class_name}} {

    /**
     * The main entry point of the application.
     * @param args
     */
    static void main(String[] args) {

        Browser.drive {

            driver.manage().window().setSize(new Dimension(1550, 775))

            // example of Page and Module classes
            to GebHomePage
            manualsMenu.open()

            // helper methods
            // scroll down to first paragraph and highlight the first 4 paragraphs one at a time.
            scrollToElement(paragraph1)
            highlightElement(paragraph1, 2)
            highlightElement(paragraph2, 2)
            highlightElement(paragraph3, 2)
            highlightElement(paragraph4, 2)

            // scroll back up to menu
            scrollToElement(manualsMenu)
            sleep(500)

            // scrape the latest geb version from the page
            def latest = latestRelease.text()
            println "latest version: $latest"

            // go the manual by clicking the link
            manualsMenu.links[0].click(TheBookOfGebPage)

            // assert that we're at the book of geb page
            at(TheBookOfGebPage)
            sleep(1000)

        }.quit()  // quit the driver
    }

    // inspired by https://elementalselenium.com/tips/65-highlight-elements
    // enhanced to take a Geb Navigator instead and loop through all WebElements in it.
    /**
     * Highlights the navigator elements with a dashed red box for the duration in seconds.
     * @param navigator A Navigator to highlight.
     * @param duration Duration in seconds the highlight will be applied.
     * @throws InterruptedException If the highlight duration is interrupted.
     */
    static void highlightElement(Navigator navigator, int duration) throws InterruptedException {

        WebDriver driver = navigator.browser.driver

        navigator.allElements().each { WebElement element ->

            String original_style = element.getAttribute("style")

            driver.executeScript(
                    "arguments[0].setAttribute(arguments[1], arguments[2])",
                    element,
                    "style",
                    "border: 2px solid red; border-style: dashed;")

            if (duration > 0) {
                sleep(duration * 1000)
                driver.executeScript(
                        "arguments[0].setAttribute(arguments[1], arguments[2])",
                        element,
                        "style",
                        original_style)
            }
        }
    }

    /**
     * Scrolls the first element of the Navigator into view.
     * @param navigator
     */
    static void scrollToElement(Navigator navigator) {

        WebDriver driver = navigator.browser.driver

        WebElement element = navigator.firstElement()
        driver.executeScript("arguments[0].scrollIntoView(true);", element)
    }

}
