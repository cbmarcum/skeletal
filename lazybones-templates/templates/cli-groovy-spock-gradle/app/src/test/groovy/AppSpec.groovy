/*
 * This Spock specification was generated by the Gradle 'init' task.
 */
package org.example

import picocli.CommandLine
import spock.lang.Specification

class AppSpec extends Specification {

    def "both operators output mutually exclusive error"() {
        setup:
        App app = new App()
        CommandLine cmd = new CommandLine(app)

        when:
        StringWriter swErr = new StringWriter()
        cmd.setErr(new PrintWriter(swErr))

        // black box testing
        int exitCode = cmd.execute("--add", "--subtract", "1", "2")

        then:
        exitCode == 2
        swErr.toString().startsWith("Error: --add, --subtract are mutually exclusive (specify only one)")
        }

    def "one operand outputs mutually exclusive error"() {
        setup:
        App app = new App()
        CommandLine cmd = new CommandLine(app)

        when:
        StringWriter swErr = new StringWriter()
        cmd.setErr(new PrintWriter(swErr))

        // black box testing
        int exitCode = cmd.execute("--add", "1")

        then:
        exitCode == 2
        swErr.toString().startsWith("Missing required parameter: '<operand2>'")
    }

    def "app can add"(String a, String b, Integer c) {
        setup:
        App app = new App()
        CommandLine cmd = new CommandLine(app)
        cmd.execute("--add", a, b)

        expect:
        app.result == c

        where:
        a | b | c
        "1"  | "2"  | 3
        "-1" | "2"  | 1
        "2"  | "-2" | 0
        "0"  | "2"  | 2
    }

    def "app can subtract"(String a, String b, Integer c) {
        setup:
        App app = new App()
        CommandLine cmd = new CommandLine(app)
        cmd.execute("--subtract", a, b)

        expect:
        app.result == c

        where:
        a | b | c
        "1"  | "2"  | -1
        "-1" | "2"  | -3
        "2"  | "-2" | 4
        "0"  | "2"  | -2
    }


}
